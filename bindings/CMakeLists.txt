cmake_minimum_required(VERSION 2.8.12)
set(ROOT_DIR ${CMAKE_CURRENT_LIST_DIR}/..)

include(${ROOT_DIR}/cmake/Environment.cmake)
include(${ROOT_DIR}/cmake/ThirdParty.cmake)

# Language bindings

file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/bindings)

option(PSMSERVICE_BUILD_CSHARP_BINDINGS "Build the C# bindings" ON)
option(PSMSERVICE_BUILD_PYTHON_BINDINGS "Build the Python bindings" ON)

# useful if multiple versions of the Python lib are installed
set(PSMSERVICE_PYTHON_VERSION "" CACHE STRING "Use this version of the Python lib for building Python bindings")

# Language bindings (Python and C#)
if(SWIG_FOUND)
    include(${SWIG_USE_FILE})

    if(PSMSERVICE_BUILD_PYTHON_BINDINGS)
        find_package(PythonLibs ${PSMSERVICE_PYTHON_VERSION} QUIET)
        if(PYTHONLIBS_FOUND)        
            unset(CMAKE_SWIG_FLAGS)            
            unset(CMAKE_SWIG_OUTDIR)
            set(CMAKE_SWIG_OUTDIR "${CMAKE_CURRENT_LIST_DIR}/python")
            
            include_directories(${PYTHON_INCLUDE_PATH})
            include_directories(${ROOT_DIR}/src/psmoveclient)
            include_directories(${ROOT_DIR}/src/psmoveprotocol)
            swig_add_library(PSMoveClient_swig_python LANGUAGE python SOURCES ${CMAKE_CURRENT_LIST_DIR}/PSMoveClient.i)
            swig_link_libraries(PSMoveClient_swig_python PSMoveClient_CAPI ${PYTHON_LIBRARIES})
            set_target_properties(_PSMoveClient_swig_python PROPERTIES FOLDER Client)
            set_target_properties(_PSMoveClient_swig_python PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/python)
            set(INFO_BUILD_PYTHON_BINDINGS "Yes (version ${PYTHONLIBS_VERSION_STRING})")
            
            #install(TARGETS _PSMoveClient_python CONFIGURATIONS Debug DESTINATION ${PSM_DEBUG_INSTALL_PATH}/bin)
            #install(TARGETS _PSMoveClient_python CONFIGURATIONS Debug DESTINATION ${PSM_RELEASE_INSTALL_PATH}/bin)
        else()
            set(INFO_BUILD_PYTHON_BINDINGS "No (libpython not found)")
        endif()
    else()
        set(INFO_BUILD_PYTHON_BINDINGS "No (disabled)")
    endif()

    if(PSMSERVICE_BUILD_CSHARP_BINDINGS)
        # PSMoveClient_swig_csharp runs SWIG to generate C# wrapper functions
        # Generated files are written to the bindings/csharp folder
        unset(CMAKE_SWIG_FLAGS)
        unset(CMAKE_SWIG_OUTDIR)
        set(CMAKE_SWIG_FLAGS -namespace "PSMoveService")        
        set(CMAKE_SWIG_OUTDIR "${CMAKE_CURRENT_LIST_DIR}/csharp")
    
        include_directories(${ROOT_DIR}/src/psmoveclient)
        include_directories(${ROOT_DIR}/src/psmoveprotocol)
        swig_add_library(PSMoveClient_swig_csharp LANGUAGE csharp SOURCES ${CMAKE_CURRENT_LIST_DIR}/PSMoveClient.i)
        swig_link_libraries(PSMoveClient_swig_csharp PSMoveClient_CAPI)
        set_target_properties(PSMoveClient_swig_csharp PROPERTIES FOLDER Client)
        set_target_properties(PSMoveClient_swig_csharp PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/csharp)
        set(INFO_BUILD_CSHARP_BINDINGS "Yes")
        install(TARGETS PSMoveClient_swig_csharp CONFIGURATIONS Debug DESTINATION ${PSM_DEBUG_INSTALL_PATH}/bin)
        install(TARGETS PSMoveClient_swig_csharp CONFIGURATIONS Debug DESTINATION ${PSM_RELEASE_INSTALL_PATH}/bin)
        
        # PSMoveClient_csharp creates a C# assembly from the files generated by PSMoveClient_swig_csharp
        file(GLOB PSMOVECLIENT_CSHARP_SRC
            "${CMAKE_CURRENT_LIST_DIR}/csharp/*.cs"
            "${CMAKE_CURRENT_LIST_DIR}/csharp/Properties/*.cs"
        )
        add_library(PSMoveClient_csharp SHARED ${PSMOVECLIENT_CSHARP_SRC})
        add_dependencies(PSMoveClient_csharp PSMoveClient_swig_csharp)
        set_target_properties(PSMoveClient_csharp PROPERTIES FOLDER Client)
        install(TARGETS PSMoveClient_csharp 
            CONFIGURATIONS Debug 
            RUNTIME DESTINATION ${PSM_DEBUG_INSTALL_PATH}/bin
            LIBRARY DESTINATION ${PSM_DEBUG_INSTALL_PATH}/lib
            ARCHIVE DESTINATION ${PSM_DEBUG_INSTALL_PATH}/lib)
        install(TARGETS PSMoveClient_csharp 
            CONFIGURATIONS Debug 
            RUNTIME DESTINATION ${PSM_RELEASE_INSTALL_PATH}/bin
            LIBRARY DESTINATION ${PSM_RELEASE_INSTALL_PATH}/lib
            ARCHIVE DESTINATION ${PSM_RELEASE_INSTALL_PATH}/lib)        
    else()
        set(INFO_BUILD_CSHARP_BINDINGS "No (disabled)")
    endif()
else()
    set(INFO_BUILD_PYTHON_BINDINGS "No (SWIG not found)")
    set(INFO_BUILD_CSHARP_BINDINGS "No (SWIG not found)")
endif()

message("")
message("  Language bindings")
message("    Python:           " ${INFO_BUILD_PYTHON_BINDINGS})
message("    C#:               " ${INFO_BUILD_CSHARP_BINDINGS})
message("")